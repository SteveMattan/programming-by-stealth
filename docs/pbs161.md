## PBS 161 of X — jq: Maths, Assignment & String Manipulation

In [the previous instalment](./pbs160) we learned to use jq as a scripting language, so we can now create complex jq filters without them becoming un-readable and un-manageable. This sets us up nicely for learning about the many ways in which jq can manipulate numbers, strings, arrays, and dictionaries. This will take a few instalments to get through, but we'll start with jq's support for maths, assignment operators in jq, and string manipulation.

## Matching Podcast Episode

TO DO

## Installment Resources

* The instalment ZIP file — [pbs161.zip](https://github.com/bartificer/programming-by-stealth/raw/master/instalmentZips/pbs161.zip)

## PBS 160 Challenge Solutions

The challenge set at the end of [the previous instalment](./pbs160) was to update the example jq script of searching the Nobel prizes by laureate name to take two additional arguments, an earliest allowable year, and a latest allowable year.

You'll find the sample solution in the instalment ZIP as `pbs160-challengeSolition-1.jq`:

```jq
# Search the Nobel Prizes data set as published by the Nobel Prize Committee
# by name and year.
# Input:    JSON as published by the Nobel Committee
# Output:   An array of prize dictionaries
# Variables:
#   $search:    The search string for matching the names
#   $minYear:   The earlist a matching prize can have been awarded
#   $maxYear:   The latest a matching prize can have been awarded
[
    .prizes[]
    | select(
        any(.laureates[]?;
            "\(.firstname) \(.surname)"
            | ascii_downcase
            | contains($search | ascii_downcase)
        )
        and (.year | tonumber) >= ($minYear | tonumber)
        and (.year | tonumber) <= ($maxYear | tonumber)
    )
]
```

This solution is almost entirely the same as the final example in the previous instalment (`pbs160b-2.jq`), but with the addition of two more conditions inside the `select`:

```jq
and (.year | tonumber) >= ($minYear | tonumber)
and (.year | tonumber) <= ($maxYear | tonumber)
```

These additional conditions both use the `tonumber` function before doing comparisons to be sure the comparisons really are numeric, and not lexical (alphabetic).

The challenge offered bonus credit for making the minimum and maximum year arguments optional. You'll find a sample solution for extra credit it in the instalment ZIP as `pbs160-challengeSolition-2.jq`:

```jq
# Search the Nobel Prizes data set as published by the Nobel Prize Committee
# by name and year.
# Input:    JSON as published by the Nobel Committee
# Output:   An array of prize dictionaries
# Variables:
#   $search:    The search string for matching the names
#   $minYear:   The earlist a matching prize can have been awarded (optional)
#   $maxYear:   The latest a matching prize can have been awarded (optional)
[
    .prizes[]
    | select(
        any(.laureates[]?;
            "\(.firstname) \(.surname)"
            | ascii_downcase
            | contains($search | ascii_downcase)
        )
        and (($ARGS.named | has("minYear") | not) // (.year | tonumber) >= ($ARGS.named.minYear | tonumber))
        and (($ARGS.named | has("maxYear") | not) // (.year | tonumber) <= ($ARGS.named.maxYear | tonumber))
    )
]
```

The only lines that have changed are the two conditions for checking the years:

```jq
and (($ARGS.named | has("minYear") | not) // (.year | tonumber) >= ($ARGS.named.minYear | tonumber))
and (($ARGS.named | has("maxYear") | not) // (.year | tonumber) <= ($ARGS.named.maxYear | tonumber))
```

The first point to note is the switch from using `$mixYear` & `$maxYear` to using `$ARGS.named.minYear` & `$ARGS.named.maxYear` — as explained in the previous instalment, this is necessary for optional arguments because of how jq compiles filters — simply mentioning a non-existent variable anywhere a jq filter will cause errors!

The second point to note is the use of the  `has()` function to detect the presence of the optional arguments by checking if `$ARGS.named` has a key named `minYear`/`maxYear`.

There are probably infinitely many ways to go about altering the action of the filter depending on whether or not the variable is present, but the approach this sample solution takes is to ensure that when either optional argument is not present the appropriate condition always returns `true`. This is achieved by first inverting the output of the `has()` function to effectively turn it into a *has not* function using the `not` function, and then using the alternate operator (`//`). Since the alternate operator only executes the filter to its right when the filter to its left returns `empty`, `null`, or `false`, this has the effect of only executing the comparison when the variable exists. When ever the variable does not exist `has()` returns `false`, which the `not` converts to `true`, which the alternate operator returns without running the filter to its right at all. Hence, the check evaluates to `true` and the non-existent argument is never processed.

## Mathematical Operators & Functions

Let's start nice and simple, the jq language supports the usual suspects in terms of mathematical operators:

| Operator | Description        | Example                      |
| -------- | ------------------ | ---------------------------- |
| `+`      | Addition           | `jq -n '1 + 1'` produces `2` |
| `-`      | Subtraction        | `jq -n '3 - 1'` produces `2` |
| `*`      | Multiplication     | `jq -n '2 * 1'` produces `2` |
| `/`      | Division           | `jq -n '4 / 2'` produces `2` |
| `%`      | Modulo (remainder) | `jq -n '5 % 3'` produces `2` |

Note the use of the `-n` flag in the examples to tell the `jq` command not to expect any input to process.

Something to notice is that the jq language does not support the increment and decrement operators `++` and `--`.

As well as these operators, the jq language also provides built in functions for performing arithemtic.

To make the examples shorter to write and easier to interpret we'll use a pair of JSON files from the instalment ZIP in our examples. First, we'll use `numbers.json` which contains a single top-level array of numbers:

```json
[
	-42,
	0,
	3.1415,
	11
]
```

And we'll also use `menu.json` which contains a top-level array of dictionaries, each contianing keys that are numeric:

```json
[
	{
		"name": "hotdogs",
		"price": 5.99,
		"stock": 143
	},
	{
		"name": "pancakes",
		"price": 3.10,
		"stock": 43
	},
	{
		"name": "waffles",
		"price": 7.50,
		"stock": 14
	}
]
```

Note that some of these functions can tolerate having non-numbers sent to them, but many will throw errors for at least some non-numeric data types, so to be safe, only send numbers through these functions. Another thing to watch out for is that even when non-numeric values don't cause errors they can produce unexpected results, e.g. the absolute value of a string represatation of a negative number is the same string, un-changed — `jq -n '"-9.999" | abs'` outputs `"-9.999"`.

| jq Function         | Description                                                  | Example                                                      |
| ------------------- | ------------------------------------------------------------ | ------------------------------------------------------------ |
| `abs`               | Outputs the absolute value of the input.                     | `jq '.[] | abs' numbers.json` outputs`42`, `0`, `3.1415` & `11` |
| `floor`             | Rounds input decimal numbers down to the integer part.       | `jq -n '3.1415 | floor'` outputs `3`, and `jq -n '9.999 | floor'` outputs `9` |
| `sqrt`              | Outputs the square root of the input.                        |                                                              |
| `min`               | Outputs the minimum value from an input array of numbers.    | `jq '. | min' numbers.json` outputs `-42`                    |
| `max`               | Outputs the maximum value from an input array of numbers.    | `jq '. | max' numbers.json` outputs `11`                     |
| `min_by(.KEY_PATH)` | Outputs the dictionary in an input array of dictionaries that has the minimum value for a given key. | `jq -c '. | min_by(.stock)' menu.json` outputs `{"name":"waffles","price":7.50,"stock":14}` |
| `max_by(.KEY_PATH)` | Outputs the dictionary in an inpuit array of dictionaries that has the maximum value for a given key. | `jq -c '. | max_by(.price)' menu.json` outputs `{"name":"waffles","price":7.50,"stock":14}` |

Those are the arithmetic functions provided by the jq language itself, but the `jq` command has a mathematical trick up its sleeve! All the standard 1, 2 & 3 argument C arithmetic functiuons from C's standard math library can be used in jq. 

For reason's I can't quite fathom the way in which one-argument C functions and two or three argument C functions get mapped to jq are not consistent. For one-argument C functions the input to the jq function gets passed as the C function's argument. But, for two and three argument C functions all arguments need to be specified in jq.

The exact functions available will depend on the version of the C libaries installed on the computer, but some widely available useful ones of note are:

| C Maths Function | Description                                                  | Example                                                      |
| ---------------- | ------------------------------------------------------------ | ------------------------------------------------------------ |
| `ceil`           | Round an input decimal number up to the nearest integer. An obvious companion for jq's built-in `floor` and C's `round` functions. | `jq -n '3.1415 | ceil'` outputs `4`, and `jq -n '9.999 | ceil'` outputs `10` |
| `round`          | Round an input decimal number up or down to the nearest integer. An obvious companion for jq's built-in `floor` and C's `ceil` functions. | `jq -n '3.1415 | round'` outputs `3`, and `jq -n '9.999 | round'` outputs `10` |
| `pow`            | Raise an input number to the power passed as the first argument. | `jq -n '2 | pow(.; 3)'` outputs `8`                          |

## Assignment Operators

In most programming languages, assignment operators are used to set the values of variables, but that's now the case in jq, instead, **assignment operators are used to set values within _the item currently being processed_**, i.e. for controling values within `.`.

The left-hand-side of an assignment operator has to be a path within `.`, e.g. `.year` to alter the `year` key of the dictionary currently being processed. The right-hand-side must be an expression that produces the value to be assigned.

### Plain -v- Update Assignments

A very important subtly is the different between the meaning of `.` on the right-hand-side of an expression depending on whether you use the *plain* assignment operator `=`, or one of the many *update-assignment* operators (`|=`, `+=`, `*=`, etc.).

With plain assignment, the value for `.` is the **same** on both sides of the operator, it is *the item currently being processed*. When using the update-assignments operators, the value for `.` is _the value for **the item currently being assigned into**_.

To see the different, consider the following simple dictionary:

```json
{
  "breakfast": "pancakes",
  "lunch": "BLT",
  "dinner": "pizza"
}
```

If that dictionary were in input to a plain-assignment, then the value of `.` on **both** the left and right hand sides would be that full dictionary, so you could set the value for `dinner` to become the same as the value for `breakfast` with the assignment `.dinner = .breakfast`.  The result of this assignment would be to change the input dictionary to become:

```json
{
  "breakfast": "pancakes",
  "lunch": "BLT",
  "dinner": "pancakes"
}
```

We can see this for ourselves with the following `jq` command:

```sh
# -n tell jq not to expect any input
jq -n '{breakfast: "pancakes", lunch: "BLT", dinner: "pizza"} | .dinner = .breakfast'
```

If we send the same dictionary to the update-assignment `.dinner |= .breakfast`, we get an error, why? Because with an update assignment, the value of `.` is the currenty value of `.dinner`, because update assignments are designed for use when you want the new value to depend on the old value.

So, if we wanted to append to our dinner we could do it the long way with, `.dinner = "\(.dinner) and nachos"`, or, we could avoid the need to specifiy the path twice and simple use `.dinner = "\(.) and nachos"`, as demonstrated by this `jq` command:

```sh
jq -n '{breakfast: "pancakes", lunch: "BLT", dinner: "pizza"} | .dinner |= "\(.) and nachos"'
```

To keep the two types of assignment clear in my head, the question I ask myself is *"do I want to set a new value, or update the existing value"* — if I want to update the existing value, I use an update assignment, and `.` becomes *the current value*, which seems like a sensible decision by the jq developers given the problem to be solved.

### Always *'by Value'*, Never *'by Reference'*

In many modern programming languages, including JavaScript, complex data types are *passed by reference* while simple values are *passed by value*.

This fact trips up many novice programmers, because it can cause *spooky action at a distance*. For example, consider the following JavaScript code, what will get logged to the console?

```javascript
let a = [1, 2, 3];
let b = a;
a.push(4);
console.log(b);
```

If JavaScript assigned `b` to be a **copy** of `a` then `b` would have `a`'s original value, `[1, 2, 3]`, but that's not what happens, because in Javascript `a` does not contain the array, it contains a **reference** to the array, so when `b` gets assigned to be the same as `a` it becomes a **reference to the same array**, so the log statement will output `[1, 2, 3 , 4]` (as you can see for yourself using your browser's JavaScript console)!

But, unlike JavaScript, **jq always coppies by value, never by reference**.

So, in jq, when you assign the value of `.a` to `.b`, then update `.b`, there is no spooky action at a distance, and demonstrated by the following simple jq script (`pbs161a.jq` in the instalment ZIP):

```jq
# Demonstrate that jq uses pass by value rather than pass by reference
# Input:    NONE
# Output:   A dictionary with keys a & b

# start with a dictrionary with one key, a, that is an array
{
    a: [1, 2, 3]
}

# update the dictionary with a new key b that is a copy of a
| .b = .a

# update a by adding a 4th value to the array
| .a[3] = 4
```

We can run this script with the command:

```sh
# -f tells jq to read its filter from a file
# -c tells jq we want compact output
jq -n -f pbs161a.jq -c
```

When we do, it produces the following output (re-flowed to save space):

```json
{"a":[1,2,3,4],"b":[1,2,3]}
```

This shows that unlike in JavaScript, in jq, modifying `.a` after setting `.b` to be equal to `.a` does not change `.b`.

### The Update Assignment Operators

As we've already seen, the generic update assignment operator is `|=`, but jq provides a suite of addtional update opperators that are in effect simply shortcuts to make your filters easier to write and read. 

| Operator | Description                               | Shortcut for |
| -------- | ----------------------------------------- | ------------ |
| `+=`     | Increment by …                            | `|= . +`     |
| `-=`     | Decrement by …                             | `|= . -`     |
| `*=`     | Multiply by …            | `|= . *`     |
| `/=`     | Divide by …               | `|= . /`     |
| `%=`     | Modulous … | `|= . %`     |
| `//=`    | Alternate assignment           | `|= . //`    |

While `jq` does not have increment or decrement operators (`++` & `--`in most languages), we can at least use `+= 1` and `-= 1` instead, as shown in this example:

```sh
jq -nc '{a: 2, b: 2} | .a += 1 | .b -= 1' # outputs {"a":3,"b":1}
```

Finally, the alternate assignment operator (`//=`) can be useful for assigning default values for dictionary keys that may or may not  be present. For example, our example Nobel Prizes data set (`NobelPrizes.json` in the instalment ZIP) does not have a `laureats` array in the dictionaries representing prizes that were not awarded — we could add an empty `laureats` array into every un-awarded prize with the command:

```sh
jq '[.prizes[] | .laureates //= []]' NobelPrizes.json
```

## Transforming Strings

As well as allowing us to transform numbers, the jq languages provides many mechanisms for transforming strings.

### 'Adding', 'Multipling' & 'dividing' Strings (Operator Overloading)

The jq language has, to use programming jargon *overloaded* some of it's operators, including the addition (`+`) operator. Overloaded operators do different things when presented with different types of data to operate on. Many programming languages use overloading, and while we may not have called it out by name when we expored JavaScript, we did see operator overloading when we used `+` both for arithmetic addition, and for string concatenation.

Note that the jq language's support of operator overloading is basic compared to what we saw with JavaScript. JavaScript's operator overloading can handle different data types on each side of the operator, and them automatically *casts* (i.e. converts) one of the two to match the other before applying the appropriate action. This requires complex rules for determining what to do for every possible combination of types. In jq things are much simpler — with just a few specific exceptions, **the types on each side of the overloaded operators must match**! 

Bringing this back to strings, like in JavaScript, jq's `+` operator is overloaded for both arithmentic addition and string concatenation  i.e. when both inputs are numbers they get added, and when both are strings they get concatenated:

```sh
# two numbers - arithmetic addition (as above)
jq -n '22 + 20' # oputputs 42

# two strings - concatenation
jq -n '"a string" + " another string"' # oputputs "a string another string"

# mismatched types
jq -n '"a string" + 42' # throws an error
```

As we'll learn later, the `+` operator is even more overloaded than this, it can also handle arrays and dictionaries!

The multiplication operator (`*`) is also overloaded, and it is one of those where a specific mis-match of types is explicitly supported — you can multiply a string by a number to concatenate it with itself that many times, in other words, to repeat it that many times:

```sh
jq -n '"Ho" * 3' # outputs "HoHoHo"
```

Finally, the division operator `/` has been overloaded to work with two strings — when a string is divided by another string, the string to the left is split on the string to the right, and an array is returned. In other words, **string division with `/` is an shortcut for the `split` function**.

```sh
jq -nc '"10:20:30" / ":"' # outputs ["10","20","30"]
```

### Built-in String Functions

The jq language provides a selection of built-in functions for manipulating strings, some of the more useful ones are listed below:

| Function         | Description                                                  | Example                                                      |
| ---------------- | ------------------------------------------------------------ | ------------------------------------------------------------ |
| `ascii_downcase` | Convert an input string to lower case.                       | `jq -n '"Somethin" | ascii_downcase'` outputs `"something"`  |
| `ascii_upcase`   | Convert an input string to upper case.                       | `jq -n '"Something" |ascii_upcase'` outputs `"SOMETHING"`    |
| `ltrimstr`       | Remove a given prefix from an input string, or return the string un-changed if the prefix is not present. | `jq -n '"DEBUG: something" | ltrimstr("DEBUG: ")'` outputs `"something"` |
| `rtrimstr`       | Remove a given postfix from an input string, or return the string un-changed if the postfix is not present. | `jq -n '"something (DEBUG)" | rtrimstr(" (DEBUG)")'` outputs `"something"` |
| `sub`            | Perform string substitution on an input string to produce the output string. A regular expression should be passed as the first argument, a replacement as the second, and optionally regular expression flags as a third. | `jq -n '"2023-11-12: Something" | sub("[0-9]{4}-[0-9]{2}-[0-9]{2}: "; "")'` outputs `"Something"`, i.e. replaces a leading timestamp with an empty string. |

## Optional Challenge

write a jq script that will take as its input the Nobel Prizes data set (`NobelPrizes.json`), and sanitises it, using the various assignment operators, to achieve the following changes:

1. Add a boolean key named `awarded` to every prize dictionary to indicate whether or not it was awarded.
2. Ensure all prize dictionaries have a `laureates` array, it should be empty for prizes that were not awarded.
3. Add a boolean key named `organisation` to each laureate dictionary, indicating whether or not the laureate is an organisation rather than a person.
4. Add a key named `displayName` to each laureate dictionary, for people it should contain the first name and last names, for organisations, just the organisation name.

## Final Thoughts

In this instalment we made a good start on our exploration of how jq can alter data, we'll keep the momentum going in the next instalment, looking at how we can alter arrays and dictionaries.
